plugins {
  id 'org.springframework.boot' version '2.4.2'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
}
group = 'com.github.dbranco'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

repositories {
  mavenCentral()
}

ext {
    imageDirPath = "$buildDir/jaguar-image"
    imageZipPath = "$buildDir/image-zip/jaguar-image.zip"
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'info.picocli:picocli-spring-boot-starter:4.6.1'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
  useJUnitPlatform()
}

task deleteOutputImage(type: Delete) {
    delete "${buildDir}/jaguar-image"
}

task jPackage(type: Exec) {
    commandLine 'cmd', '/c', 'jpackage',
          '--type', 'app-image',
          '--name', 'jaguar',
          '--input', jar.destinationDirectory.asFile.get(),
          '--dest', "${buildDir}/jaguar-image",
          '--main-jar', jar.archiveFileName.get(),
          '--win-console'
    environment << [JPACKAGE_DEBUG: true]
}

jPackage.dependsOn(deleteOutputImage)
assemble.dependsOn(jPackage)
